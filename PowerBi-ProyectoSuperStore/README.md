# üè¨ Superstore Analytics Project  
### Presente
La empresa Superstore, dedicada a la venta de muebles, elementos de oficina y tecnolog√≠a, gestionaba su informaci√≥n mediante una sola tabla en Excel, claramente no esta normalizada y con atributos que no aportan a su an√°lisis. Esto dificultaba el an√°lisis profundo de ventas, clientes y m√©todos de operaci√≥n.  

### Prop√≥sito del an√°lisis:
Valorar el presente de la empresa respecto a sus ventas, ya sea por lugar, categor√≠a o sus ganancias, adem√°s de analizar c√≥mo se comportan los clientes.  

Se espera responder:
- ¬øCu√°les son las regiones y segmentos donde se vende m√°s?
- ¬øQu√© m√©todo de pago se usa m√°s en las ventas?
- ¬øCu√°les son las ganancias de los dos a√±os, adem√°s de ventas y sus cantidades?
- ¬øQu√© m√©todos de env√≠o deja m√°s ventas?
- ¬øQue categor√≠as y subcategor√≠as se venden m√°s?
- ¬øCu√°l es la comparativa de ventas entre los dos a√±os?  

Respecto al comportamiento de los clientes:  
- ¬øCuantos clientes han comprado por regi√≥n?
- ¬øQue categor√≠as compran m√°s los clientes y si hay alguna relaci√≥n entre compras?
- ¬øQuienes son los clientes que m√°s ventas han dejado por regi√≥n?

### :computer: Tecnolog√≠as usadas:  
- Excel (fuente de datos inicial)
- Power Query (limpieza y transformaci√≥n)
- Power BI (modelado y visualizaciones)
- DAX (medidas y tablas calculadas)

### üõ†Ô∏è Normalizaci√≥n y modelado  
**1.** Se analiz√≥ la tabla de Excel anexada (‚ÄúSuperStore Sales DataSet.xlsx‚Äù) usando power query para ver si hab√≠an atributos que no ten√≠an pie, adem√°s de buscar errores, tipos de datos err√≥neos o nulos.  
**2.** Se normaliz√≥ la tabla usando la 1NF, la 2NF y la 3NF, obteniendo el siguiente modelo relacional en Snowflake:  

   <img src="imgMR.png" alt="Logo del proyecto" width="900"/>  
   
Teniendo en cuenta este modelo, se procede a generar dashboards para el storytelling.  

### üìä Dashboards creados

**1.** Se cre√≥ un dashboard inicial usando los atributos del modelo relacional llamado Sales Report, donde se observa algunos datos de ventas respecto a:  

   **a.** Regi√≥n.<br>
   **b.** Segmento.<br>
   **c.** M√©todo de pago.<br>
   **d.** M√©todo de env√≠o.<br>
   **e.** Categor√≠a.<br>
   **f.** Subcategor√≠a.<br>
   **g.** Por ciudad usando un Mapa.<br>
   **h.** Por a√±o d√≠a a d√≠a.<br>
   **i.** Total general.<br>  
   
**2.** Se gener√≥ otro dashboard llamado Sales Report 2, donde se usa la herramienta de forecasting para analizar 15 d√≠as futuros con una estacionalidad de 7 puntos, puesto que los picos m√°s bajos se presentan cada 7 d√≠as. Tambi√©n se generan otros visuales que si usan DAX:  

   **a.** Se gener√≥ un visual para el acumulado de cada a√±o, se us√≥ la siguiente sentencia DAX:<br>  
   
   ```sql
	   Cumulative Sales = 
	      CALCULATE (
	          SUM ( Order_Product[Sales] ),
	          FILTER (
	              ALL ( 'Order'[Order Date] ),
	              'Order'[Order Date] <= MAX ( 'Order'[Order Date] )
	          )
	      )
   ```

  Se suman las ventas teniendo en cuenta todas las fechas (por eso all(order)). Luego dentro del gr√°fico de l√≠neas se ponen los par√°metros de los ejes.      

**b.** El otro visual es un medidor de ventas del 2020 respecto al 2019, se usaron dos sentencias DAX b√°sicas para poder conocer las ventas de cada a√±o:<br>  
	
   ```sql
    Total acumulado 2019 = 
      CALCULATE(
      	SUM('Order_Product'[Sales]),
      	'Order'[Order Date]<DATE(2020,01,01)
      )

    Total acumulado 2020 = 
      CALCULATE(
      	SUM('Order_Product'[Sales]),
      	'Order'[Order Date]>=DATE(2020,01,01)
      )
   ```  
	
**3.** En el tercer dashboard llamado Dashboard Customers se realiza un entendimiento de los patrones de compra de los clientes, donde se tienen que crear en algunas ocasiones tablas calculadas o simplemente una medida, a continuaci√≥n, se muestra:  

   **a.** Clientes de segmentaci√≥n corporaci√≥n que enviaron por primera clase y pagaron online:  
   
   ```sql
   OnlineXCorporateXFirstClass = 
		CALCULATE(
			COUNTA('Order'[id_order]),
			PaymentMode[Payment Mode] = "Online",
			ShipMode[Ship Mode] = "First Class",
			Segment[Segment] = "Corporate"
		)
   ```
   Se filtra de manera b√°sica con un calculate, donde cada filtro funciona como un 'and'.<br>
   
   **b.** Promedio de d√≠as de entrega.<br>
   ```sql
   DeliveryDays = 
      AVERAGEX (
          'Order',
          DATEDIFF ( 'Order'[Order Date], 'Order'[Ship Date], DAY )
      )
   ```  
   Se toma la fecha de entrega menos la de env√≠o de cada orden, y se promedia.  
   
   **c.** Saber cu√°les son los clientes que compran en el este o en el oeste, m√°s no en ambos lugares, y que pagaron toda su orden con solo un m√©todo de pago agrupados por categor√≠as.
   ```sql
    categoriasEastWest = 
    	VAR clientesXcategoriaXregionXpay = SUMMARIZE(
    		ADDCOLUMNS(
    			Order_Product,
    			"cliente", RELATED(Customer[id_customer]),
    			"categoria", RELATED(CategoryProduct[Category]),
    			"TheRegion", RELATED(Region[Region])
    		),
    		[cliente],
    		[categoria],
    		[TheRegion],
    		Order_Product[id_paymentmode_fk]
    	)
    
    	VAR clienteXregion = SUMMARIZE(
    		clientesXcategoriaXregionXpay,
    		[cliente],
    		[TheRegion]
    	)
    	VAR agruparRegion = FILTER(
    		GROUPBY(
    			clienteXregion,
    			[cliente],
    			"isOne",
    			COUNTX(
    				CURRENTGROUP(),
    				[TheRegion]
    			)
    		),
    		[isOne] = 1
    	)
    
    	VAR joinAgrupacion = FILTER(
    		NATURALINNERJOIN(
    			clientesXcategoriaXregionXpay,
    			agruparRegion
    		),
    		[TheRegion] = "East" || [TheRegion] = "West"
    	)
    
    	VAR clientexpaymode = SUMMARIZE(
    		joinAgrupacion,
    		[cliente],
    		[id_paymentmode_fk]
    	)
    
    	VAR agrupacionPaymode = FILTER(
    		GROUPBY(
    			clientexpaymode,
    			[cliente],
    			"isJustOne",
    			COUNTX(
    				CURRENTGROUP(),
    				[id_paymentmode_fk]
    			)
    		),
    		[isJustOne] = 1
    	)
    
    	VAR agrupacionClienteXCat = SUMMARIZE(
    		NATURALINNERJOIN(
    			joinAgrupacion,
    			agrupacionPaymode
    		),
    		[categoria],
    		[cliente]
    	)
    
    	RETURN
    		GROUPBY(
    			agrupacionClienteXCat,
    			[categoria],
    			"Total",
    			COUNTX(
    				CURRENTGROUP(),
    				[cliente]
    			)
    		)	
   ```  
   En este caso, se cre√≥ una tabla calculada, donde lo que se hace es:
   1. Agrupar cliente, categor√≠a y regi√≥n.
   2. Crear una sub tabla a partir de (i) que agrupa cliente con regi√≥n.
   3. Buscar los clientes que pertenecen a una sola regi√≥n de (ii).
   4. Hacer join del anterior resultado (iii), filtrandola para que solo queden los de East o West.
   5. Crear una sub tabla a partir de (i) que agrupa cliente con m√©todo de pago.
   6. Con (v) buscar que clientes que tengan un solo m√©todo de pago.
   7. Hacer join del anterior resultado (vi) con (iv).
   8. Se agrupa (vii) por categor√≠a y la cantidad de clientes por categor√≠a seg√∫n lo requerido.  
   
   **d.** Saber cu√°ntos clientes y en qu√© porcentaje compraron de a una categor√≠a, de a dos o de a tres categor√≠as.  
   ```sql
    clientesCategoriasUnicas = 
	VAR CustomerCategory =
	ADDCOLUMNS(
		Order_Product,
		"Categoria", RELATED(CategoryProduct[Category]),
		"Cliente", RELATED(Customer[id_customer])
	)

	VAR agruparclientecategoria =
	SUMMARIZE(
		CustomerCategory,
		[Cliente],
		[Categoria]
	)
 
	VAR tablaCategoriasPorCliente =
	GROUPBY(
		agruparclientecategoria,
		[Cliente],
		"CategoriasCompradas",
		COUNTX(
			CURRENTGROUP(),
			[Categoria]
		)
	)
 
	RETURN
		GROUPBY(
			tablaCategoriasPorCliente,
			[CategoriasCompradas],
			"Valor",
			COUNTX(
				CURRENTGROUP(),
				[Cliente]
			)
		)
   ```  
   En este caso, se cre√≥ una tabla calculada, donde lo que se hace es:
   1. A√±adir a Order_Product el nombre de la categoria y el id del cliente 
   2. Con (i), se agrupa por cliente y categor√≠a.
   3. Con (ii), se agrupa por cliente y se cuenta la cantidad de categor√≠as que tiene cada cliente.
   4. Con (iii), retorna agrupando por cantidad de categor√≠as y contando la cantidad de clientes por cada una de ellas.
 
   **e.** Saber cu√°ntos clientes y en qu√© porcentaje compraron muebles o elementos de oficina, pero no ambas al tiempo. Pueden haber comprado tecnolog√≠a en ambas ocasiones. Tambi√©n Saber cu√°ntos clientes compraron muebles solamente o muebles m√°s tecnolog√≠a. Pueden haber comprado elementos de oficina tambi√©n. En ambos casos puede haber una soluci√≥n en com√∫n donde seg√∫n sea el caso se puede hacer unos ajustes. La soluci√≥n general es:
   
   ```sql
  ClientesFurnitureUOffice = 
	VAR clienteXproducto = SUMMARIZE(
		ADDCOLUMNS(
			Order_Product,
			"cliente", RELATED(Customer[id_customer]),
			"categoria", RELATED(CategoryProduct[Category])
		),
		[cliente],
		[categoria]
	)

	VAR agrupador =
	GROUPBY(
		clientesXcategoria,
		[clientes],
		"IsFurniture",
		COUNTX(
			CURRENTGROUP(),
			IF(
				[categorias] = "Furniture",
				[categorias],
				BLANK()
			)
		),
		"isTechnology",
		COUNTX(
			CURRENTGROUP(),
			IF(
				[categorias] = "Technology",
				[categorias],
				BLANK()
			)
		),
		"isOfficeSupplies",
		COUNTX(
			CURRENTGROUP(),
			IF(
				[categorias] = "Office Supplies",
				[categorias],
				BLANK()
			)
		)
	)

	RETURN
		FILTER(agrupador,[isFurniture]+[isOfficeSup]=1)
   ```  
   En este caso, se cre√≥ una tabla calculada, que soluciona la primera parte del problema, donde lo que se hace es:
   1. A√±adir a Order_Product el nombre de la categoria y el id del cliente 
   2. Con (1), se agrupa por cliente, y se crean tres atributos o columnas donde si es 1, es porque tiene la categor√≠a y si es 0, nunca la compr√≥. Tener en cuenta que solo hay tres categor√≠as.
   3. En el retorno se filtra para que solo haya clientes con muebles (Furniture) o con elementos de oficina (Office Supplies), pero no ambas. Se suma puesto que si tiene solamente muebles, la suma debe dar 1, si tiene solo elementos de oficina, tambi√©n da 1, sin embargo si tiene ambas va a dar 2 o si no tiene ninguna, da 0, por lo cu√°l es un filtro adecuado.
   4. Si quisiera solucionar la segunda parte del problema, siempre tiene que haber muebles, sin importar si hay tecnolog√≠a o no en el retorno:
         
 ```sql
 RETURN
		FILTER(agrupador,[IsFurniture] = 1)
 ```
**f.** Saber cu√°l es el cliente que m√°s compr√≥ por regi√≥n y su valor. 
   
   ```sql
 clientesMaxRegion = 
	VAR orderXclienteXregion = SUMMARIZE(
		ADDCOLUMNS(
			Order_Product,
			"cliente", RELATED(Customer[id_customer]),
			"theregion", RELATED(Region[Region])
		),
		[cliente],
		[theregion],
		"suma", SUM(Order_Product[Sales])
	)


	VAR clientesMaxPorRegion =
	ADDCOLUMNS(
		SUMMARIZE(
			orderXclienteXregion,
			[theregion]
		),
		"cliente_max", MAXX(
			FILTER(
				orderXclienteXregion,
				[theregion] = EARLIER([theregion])
			),
			[suma]
		)
	)

	VAR totales = SELECTCOLUMNS(FILTER(
		NATURALINNERJOIN(
			orderXclienteXregion,
			clientesMaxPorRegion
		),
		[suma] = [cliente_max]
	),[cliente],[theregion],[cliente_max])

	RETURN
		totales
   ```  
   En este caso, se cre√≥ una tabla calculada, donde lo que se hace es:
   1. A√±adir a Order_Product el nombre de la categoria y el id del cliente y luego agrupar por cliente, regi√≥n y valor de ventas.
   2. Usando (1), por cada regi√≥n se va a buscar el valor m√°s alto de venta.
   3. Con los valores de (2), se hace join donde la suma de (1) coincida con la de (2) y se selecciona el cliente, la regi√≥n y el valor del join. Se procede a retornar.

### Conclusiones

**1.** La regi√≥n oeste (West) es la que m√°s ventas gener√≥ aportando el 33.37% de las ventas en total, segudo del este (East) con 28.75%, la regi√≥n central (Central) con 21.78% y finalizando con el sur (South) con 16.1%. Tambi√©n coincide con la cantidad de clientes que compraron por regi√≥n, aunque respecto a los m√°ximos compradores por regi√≥n, el que m√°s valor tiene de los cuatro es el del este, seguidos por el del oeste, el centro y finalizando con el del sur.  
**2.** La segmentaci√≥n consumidor (Consumer) es la que m√°s ventas gener√≥ aportando el 48.09% del total de ventas.  
**3.** El m√©todo de pago contra entrega (COD) es el que m√°s ventas gener√≥ aportando el 42.62% del total de ventas.  
**4.** El gr√°fico de l√≠neas de ventas por a√±os refleja una superioridad de ventas del 2020 respecto al 2019, pero en el gr√°fico de ganancias difiere en algunos momentos, donde el 2019 se impone respecto al 2020.  
**5.** El modo de env√≠o Standard Class fu√© el que mas ventas gener√≥ aportando el 58.27% respecto al total.  
**6.** Los elementos de oficina (Office Supplies) generaron el 41.11% de las ventas en total.  
**7.** Los tres productos que m√°s valor de ventas generaron son los tel√©fonos (15.01%), las sillas (13.89%) y las carpetas (13.36%).  
**8.** Los tres productos con m√°s unidades vendidas son de la subcategor√≠a de accesorios (Logitech¬†P710e Mobile Speakerphone: 66 unidades), mesas (Chromcraft Round Conference Tables: 59 unidades) y sillas (Situations Contoured Folding Chairs, 4/Set: 47 unidades).  
**9.** El 54.2% de los clientes compr√≥ de las tres categorias, mientras que el 32.21% compr√≥ de a dos, y el 13.58% compr√≥ de a una.  
**10.** Si el cliente compra exclusivamente o muebles (Furniture) o elementos de oficina (Office Supplies), solo hay 198 clientes, y de estos clientes, solo el 10.61% compr√≥ los muebles, mientras que el 89.39% compr√≥ los elementos de oficina. Diferente en el caso de que se compraran ambos al tiempo, donde hay 562 clientes.  
**11.** Si el cliente compra muebles (Furniture) sin tecnolog√≠a (Technology), o si compra muebles y tecnologia, tiene 583 clientes, donde el 26.42% solo compr√≥ muebles, mientras que el 73.58% compr√≥ ambas categor√≠as.  
**12.** Los clientes que son de la regi√≥n este (East) u oeste (West), m√°s no de ambas, que pagaron con solo un m√©todo de pago son 35 (4.53%) de 773.  
**13.** Los clientes del segmento Corporate que pagaron primera clase y Online son solo 73 (9.44%) de 773.  
**14.** El promedio de d√≠as de entrega en general son 3.94 d√≠as.  
**15.** El promedio de valor de venta por producto es de $265.35.

   
